apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

/**
 * Use the property (key/value pairs format) file secrets.properties
 * to configure api keys for your project.
 * Here is the list of necessary keys:
 * - DEBUG_THE_MOVIE_DB_API_KEY
 * - RELEASE_THE_MOVIE_DB_API_KEY
 */
def Properties projectProperties = new Properties()
projectProperties.load(new FileInputStream(new File(projectDir, '/secrets.properties')))

final String THE_MOVIE_DB_API_KEY_NAME = "THE_MOVIE_DB_API_KEY"

ext {
    ANDROID_SUPPORT_LIBS_VERSION = '26.0.1'
    DAGGER_VERSION = '2.10'
}

android {
    compileSdkVersion 26
    buildToolsVersion ANDROID_SUPPORT_LIBS_VERSION
    defaultConfig {
        applicationId "br.com.ecarrara.popularmovies"
        minSdkVersion 19
        targetSdkVersion 26
        versionCode 2
        versionName "1.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            buildConfigField "String", THE_MOVIE_DB_API_KEY_NAME, "\"" + projectProperties.DEBUG_THE_MOVIE_DB_API_KEY + "\""
        }

        release {
            buildConfigField "String", THE_MOVIE_DB_API_KEY_NAME, "\"" + projectProperties.RELEASE_THE_MOVIE_DB_API_KEY + "\""

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')

    // Android Support Libraries
    compile "com.android.support:appcompat-v7:${ANDROID_SUPPORT_LIBS_VERSION}"
    compile "com.android.support:recyclerview-v7:${ANDROID_SUPPORT_LIBS_VERSION}"
    compile "com.android.support:cardview-v7:${ANDROID_SUPPORT_LIBS_VERSION}"
    compile "com.android.support:design:${ANDROID_SUPPORT_LIBS_VERSION}"

    // Networking Libs
    compile 'com.google.code.gson:gson:2.8.0'
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.3.0'

    // Image Handling
    compile 'com.squareup.picasso:picasso:2.5.2'

    // Reactive Libs
    compile 'io.reactivex.rxjava2:rxjava:2.0.4'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'

    // Auto Values
    annotationProcessor 'com.google.auto.value:auto-value:1.2-rc1'
    annotationProcessor 'com.ryanharter.auto.value:auto-value-gson:0.4.5'
    provided 'com.ryanharter.auto.value:auto-value-gson:0.4.5'

    // Dependency Injection
    compile 'com.jakewharton:butterknife:8.5.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'

    compile "com.google.dagger:dagger:${DAGGER_VERSION}"
    annotationProcessor "com.google.dagger:dagger-compiler:${DAGGER_VERSION}"


    // Test Dependencies
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'

    // Mocking Libs
    testCompile 'com.squareup.okhttp3:mockwebserver:3.3.0'
    testCompile 'org.mockito:mockito-core:2.6.5'
}
